PREFIX ragno: <http://purl.org/stuff/ragno/>
PREFIX semem: <http://purl.org/stuff/semem/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?id ?prompt ?output ?embedding ?timestamp ?accessCount ?concepts ?decayFactor ?memoryType ?sourceType
FROM <{{graphName}}>
WHERE {
    {
        # New format: semem:Interaction objects
        ?interaction a semem:Interaction ;
            semem:id ?id ;
            semem:prompt ?prompt ;
            semem:output ?output ;
            semem:embedding ?embedding ;
            semem:timestamp ?timestamp ;
            semem:accessCount ?accessCount ;
            semem:decayFactor ?decayFactor ;
            semem:memoryType ?memoryType .
        OPTIONAL { ?interaction semem:concepts ?concepts }
        OPTIONAL { ?interaction semem:sourceType ?sourceType }
    }
    UNION
    {
        # Old format: ragno:Element objects with legacy embedding property
        ?interaction a ragno:Element ;
            ragno:embedding ?embedding .
        OPTIONAL { ?interaction skos:prefLabel ?prompt }
        OPTIONAL { ?interaction ragno:content ?content }
        OPTIONAL { ?interaction dcterms:created ?timestamp }
        BIND(CONCAT("element-", SUBSTR(STR(?interaction), 1+STRLEN(STR(?interaction))-8)) AS ?id)
        BIND(0 AS ?accessCount)
        BIND("[]" AS ?concepts)
        BIND(1.0 AS ?decayFactor)
        BIND("short-term" AS ?memoryType)
        # Use prompt as output when ragno:content is null (for simple definitions)
        BIND(IF(BOUND(?content), ?content, ?prompt) AS ?output)
    }
}